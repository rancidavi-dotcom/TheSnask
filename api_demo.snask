import "blaze";
import "json";
import "os";

// API simples para testar no Insomnia/Postman/curl.
// Rotas:
//   GET  /health
//   GET  /echo?msg=...
//   POST /sum    (body JSON: {"a": num, "b": num})
//   GET  /items
//   POST /items  (body JSON: {"name": str})

fun res_json(status, obj)
    // resposta padrÃ£o: { status, headers, body }
    let r = json::new_object();
    json::set(r, "status", status);

    let h = json::new_object();
    json::set(h, "content-type", "application/json");
    json::set(r, "headers", h);

    json::set(r, "body", json::pretty(obj));
    return r;

fun res_text(status, text)
    let r = json::new_object();
    json::set(r, "status", status);

    let h = json::new_object();
    json::set(h, "content-type", "text/plain");
    json::set(r, "headers", h);

    json::set(r, "body", text);
    return r;

fun get_items_path()
    return "./api_items.json";

fun load_items()
    let p = get_items_path();
    if os::exists(p)
        let data = os::read_file(p);
        if len(data) > 0
            return json::parse(data);
    // default: [] como obj com keys "0..n-1"
    return json::new_array();

fun save_items(items)
    let p = get_items_path();
    os::write_file(p, json::pretty(items));
    return true;

fun arr_len(arr)
    mut i = 0;
    while true
        let k = json::stringify(i);
        let v = json::get(arr, k);
        if is_nil(v)
            return i;
        i = i + 1;

fun arr_push(arr, value)
    let n = arr_len(arr);
    json::set(arr, json::stringify(n), value);
    return n;

fun health_handler(method, path, query, body, cookie)
    let o = json::new_object();
    json::set(o, "ok", true);
    json::set(o, "service", "snask-api");
    return res_json(200, o);

fun echo_handler(method, path, query, body, cookie)
    mut msg = blaze_qs_get(query, "msg");
    if is_nil(msg)
        msg = "";
    let o = json::new_object();
    json::set(o, "msg", msg);
    return res_json(200, o);

fun sum_handler(method, path, query, body, cookie)
    if is_nil(body)
        return res_text(400, "missing body");
    let req = json::parse(body);
    let a = json::get(req, "a");
    let b = json::get(req, "b");
    if is_nil(a)
        return res_text(400, "body must have a and b");
    if is_nil(b)
        return res_text(400, "body must have a and b");
    let o = json::new_object();
    json::set(o, "result", a + b);
    return res_json(200, o);

fun items_get_handler(method, path, query, body, cookie)
    let items = load_items();
    let o = json::new_object();
    json::set(o, "items", items);
    return res_json(200, o);

fun items_post_handler(method, path, query, body, cookie)
    if is_nil(body)
        return res_text(400, "missing body");
    let req = json::parse(body);
    let name = json::get(req, "name");
    if is_nil(name)
        return res_text(400, "body must have name");

    let items = load_items();
    let item = json::new_object();
    json::set(item, "id", os::random_hex(8));
    json::set(item, "name", name);
    arr_push(items, item);
    save_items(items);

    return res_json(201, item);

class main
    fun start()
        let routes = blaze::new();
        blaze::handler_method(routes, "GET", "/health", "health_handler");
        blaze::handler_method(routes, "GET", "/echo", "echo_handler");
        blaze::handler_method(routes, "POST", "/sum", "sum_handler");
        blaze::handler_method(routes, "GET", "/items", "items_get_handler");
        blaze::handler_method(routes, "POST", "/items", "items_post_handler");

        // porta (bind em 0.0.0.0)
        blaze::run(3000, routes);
